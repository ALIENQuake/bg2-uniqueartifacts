PRINT ~Compatibility:~

//Randomiser - should be installed after


//Rogue Rebalancing 3.8+
ACTION_IF (FILE_EXISTS_IN_GAME ~override/rr#aran2.cre~) THEN BEGIN
  PRINT ~Rogue Rebalancing:~
  ACTION_DEFINE_ASSOCIATIVE_ARRAY source_item_compat BEGIN
    RR#ARAN2.CRE => SW1H10  // short sword of backstabbing  |  Aran
    RENAL.CRE => G_DUMMY // Renal no longer has SSoB
    RENAL.DLG => G_DUMMY // Renal no longer has SSoB
  END
END


//Sword Coast Stratagems
ACTION_IF (FILE_EXISTS ~override/dw#vecna.mrk~) THEN BEGIN
  PRINT ~Sword Coast Stratagems:~
  ACTION_DEFINE_ASSOCIATIVE_ARRAY source_item_compat BEGIN
    GORLIC01.CRE => WA2ROBE  // Robe of Vecna  |  Amekthran lich
  END
END


//Tortured Souls
ACTION_IF (FILE_EXISTS_IN_GAME ~kachi01.wav~) BEGIN
  PRINT ~Tortured Souls:~
  COPY_EXISTING ~wmart1.sto~ ~override/wmart1.sto~ //Kachiko' wakisashi - remove from Joluv
    LPF DELETE_STORE_ITEM
      STR_VAR item_to_delete ~wawak~
    END
  BUT_ONLY
END


//BG2 Fixpack
ACTION_IF FILE_EXISTS_IN_GAME ~cdbehbla.pro~ BEGIN
  PRINT ~BG2 Fixpack:~

  //Suryris's Blade - Fixpack deletes all but Ribald's copy. Removing Ribald's and restoring Lizard man captain's (Sphere)
  COPY_EXISTING ~bazliz04.cre~ ~override~
    LPF DELETE_CRE_ITEM
      STR_VAR item_to_delete ~halb03~
    END
    REPLACE_CRE_ITEM ~halb07~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP TWOHANDED
  BUT_ONLY

  //Long Sword +2: 'Varscona' - Fixpack deletes all but Ribald's copy. Leaving at Ribald, because all other copies are in chapter 6
END


//Tactics
ACTION_IF (FILE_EXISTS_IN_GAME ~gblich01.cre~) BEGIN
  PRINT ~Tactics:~
  ACTION_DEFINE_ASSOCIATIVE_ARRAY source_item_compat BEGIN
    GBLICH01.CRE => CLCK05  // Cloak of Balduran  | Lich in the Docs
  END
END


//Thalantyr item upgrade - forbid Ring +1 upgrade
ACTION_IF (FILE_EXISTS_IN_GAME ~thsw2h01.itm~) BEGIN
  PRINT ~Thalantyr item upgrade:~
  OUTER_SET offending_state = STATE_WHICH_SAYS ~Show me what you have, and I'll tell you if it can be altered.~ FROM ~thalan.dlg~

<<<<<<<< .../uniqueartifacts-inlined/thalan.d
REPLACE_TRANS_TRIGGER thalan BEGIN %offending_state% END BEGIN END
  ~PartyHasItem("ring06")~
  ~False()~
>>>>>>>>

  COMPILE ~.../uniqueartifacts-inlined/thalan.d~ //Make ring +1 branch always false
END


//Daulmakan item pack: More work for Cromwell - forbid Ring +1 upgrade
ACTION_IF (FILE_EXISTS_IN_GAME ~d1ilbrat.itm~) BEGIN
  PRINT ~Daulmakan item pack:~
  OUTER_SET offending_state = 13  //Hmmmn... I couldn't rightly say.  Let me have a look into yer goods, then.  A minute of rummagin' and I'll know fer sure, aye?

<<<<<<<< .../uniqueartifacts-inlined/wsmith01.d
REPLACE_TRANS_TRIGGER wsmith01 BEGIN %offending_state% END BEGIN END
  ~PartyHasItem("ring06")~
  ~False()~
>>>>>>>>

  COMPILE ~.../uniqueartifacts-inlined/wsmith01.d~ //Make ring +1 branch always false
END


//Ruad item upgrade - forbid Ring +1 upgrade
ACTION_IF (FILE_EXISTS_IN_GAME ~m#ruad.dlg~) BEGIN
  PRINT ~Ruad Ro'fhessa:~
  OUTER_SET offending_state = STATE_WHICH_SAYS ~Just tell me what you have, and I'll tell you if it can be made better.~ FROM ~m#ruad.dlg~

<<<<<<<< .../uniqueartifacts-inlined/ruad.d
REPLACE_TRANS_TRIGGER m#ruad BEGIN %offending_state% END BEGIN END
  ~PartyHasItem("ring06")~
  ~False()~
>>>>>>>>

  COMPILE ~.../uniqueartifacts-inlined/ruad.d~ //Make ring +1 branch always false
END


//Almateria's restoration pack
ACTION_IF (MOD_IS_INSTALLED ~setup-arestorationp.tp2~ ~3~) BEGIN  //Restored items
  PRINT ~Almateria's restoration pack:~
  ACTION_DEFINE_ASSOCIATIVE_ARRAY source_item_compat BEGIN
    VIEKANG.CRE => BOOT08  // Boots of Phasing  |  Viekang SoA
    SARVIE01.CRE => BOOT08  // Boots of Phasing  |  Viekang ToB
    HABIB.CRE => BOOT10 // Boots of Lightning speed  |  Habib
    HABIB2.CRE => BOOT10 // Boots of Lightning speed  |  Habib
  END
  OUTER_SPRINT ffbart_boot ~boot07~ // for custom.tph, Thunderburp's boots for Mazzy
END
