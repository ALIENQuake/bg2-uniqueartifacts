ACTION_IF GAME_INCLUDES ~bg1~ BEGIN //BGT or BGEE

  //small bonus: Boots of Stealth from Nadarin (quest to kill basilisk in Baldur's Gate) replaced with Boots of the West
  COPY_EXISTING ~nadari.cre~ ~override~
    ADD_CRE_ITEM ~misc8j~ #0 #0 #0 ~NONE~ ~INV10~
  BUT_ONLY
<<<<<<<< .../uniqueartifacts-inlined/nadari.d
REPLACE_ACTION_TEXT ~nadari~
  ~GiveItem("boot02",LastTalkedToBy)~
  ~GiveItem("misc8j",LastTalkedToBy)~
>>>>>>>>
  COMPILE ~.../uniqueartifacts-inlined/nadari.d~

  //Kondar: make unstealable from Aldeth
  COPY_EXISTING ~aldeth.cre~ ~override~
    LPF ADD_CRE_ITEM_FLAGS
      STR_VAR
        item_to_change = ~sw1h03~
        flags = ~UNSTEALABLE~
    END
  BUT_ONLY

  //Ordulinian reward for blood deeds switched from Cloak of non-detection to Cloak of protection
<<<<<<<< .../uniqueartifacts-inlined/orduli.d
REPLACE_ACTION_TEXT ~orduli~ ~clck06~ ~clck01~
>>>>>>>>
  COMPILE ~.../uniqueartifacts-inlined/orduli.d~

END

//small bonus 2: Boots of Stealth from Samuel Thunderburp (for Mazzy) replaced with Boots of Lightning Speed or Boots of Elvenkind
<<<<<<<< .../uniqueartifacts-inlined/ffbart.d
REPLACE_ACTION_TEXT ~ffbart~
  ~GiveItemCreate("boot06",Player1,0,0,0)~
  ~GiveItemCreate("%ffbart_boot%",Player1,0,0,0)~
>>>>>>>>
ACTION_IF NOT (VARIABLE_IS_SET ffbart_boot) BEGIN // Boots of Elvenkind, set in compatibility - Almateria restored items
  OUTER_SPRINT ffbart_boot ~boot10~ // default to Boots of Ligtning speed
END
COMPILE EVAL ~.../uniqueartifacts-inlined/ffbart.d~
