// if installing expanded, use BGEE behaviour: hobgoblin will appear only once in either Ulcaster or Trade Way North
<<<<<<<< .../ua-inlined/g_hgzhur.baf
  IF
    OnCreation()
    Global("g_hobgzhur_created", "GLOBAL", 0)
  THEN
    RESPONSE #1
      SetGlobal("g_hobgzhur_created", "GLOBAL", 1)
  END
  IF
    OnCreation()
    Global("g_hobgzhur_created", "GLOBAL", 1)
  THEN
    RESPONSE #1
      DestroySelf()
  END
>>>>>>>>


ACTION_IF GAME_IS ~bgee eet~ BEGIN
  PRINT @35

  // BGEE replaces boot02 with a special version, we're reverting it back
  COPY_EXISTING ~hobgzhur.cre~ ~override~
    LPF REPLACE_CRE_ITEM_MATCH STR_VAR match_item = ~boot02zh~ new_item = ~boot02~ END
  BUT_ONLY

  // revert Zhurlong's dialog to BG1 behaviour, Zhurlong will accept any Boots of Stealth
  COPY_EXISTING ~zhurlo.dlg~ override
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~boot02zh~ ~boot02~
    END
  BUT_ONLY

  // If expanded, ensure only one hobgobling appears, as in BGEE
  ACTION_IF expanded == 1 BEGIN
    // enable new script
    COPY_EXISTING ~hobgzhur.cre~ ~override~
      READ_ASCII CRE_script_general script
    BUT_ONLY
    ACTION_IF FILE_EXISTS_IN_GAME ~%script%.bcs~ BEGIN
      EXTEND_TOP ~%script%.bcs~ ~.../ua-inlined/g_hgzhur.baf~
    END ELSE BEGIN
      COMPILE ~.../ua-inlined/g_hgzhur.baf~
      COPY_EXISTING ~hobgzhur.cre~ ~override~
        WRITE_ASCII CRE_script_general ~g_hgzhur~
      BUT_ONLY
    END
  END

END
