ACTION_IF GAME_IS ~bg1 totsc~ BEGIN //missing triggers, scripts won't recompile without them
  APPEND ~trigger.ids~ ~0x4076 OpenState(O:Object*,I:Open*BOOLEAN)~ UNLESS ~0x4076 OpenState(O:Object\*,I:Open\*BOOLEAN)~
  APPEND ~trigger.ids~ ~0x4077 NumItems(S:ResRef*,O:Object*,I:Num*)~ UNLESS ~0x4077 NumItems(S:ResRef\*,O:Object\*,I:Num\*)~
  APPEND ~trigger.ids~ ~0x4078 NumItemsGT(S:ResRef*,O:Object*,I:Num*)~ UNLESS ~0x4078 NumItemsGT(S:ResRef\*,O:Object\*,I:Num\*)~
  APPEND ~trigger.ids~ ~0x4079 NumItemsLT(S:ResRef*,O:Object*,I:Num*)~ UNLESS ~0x4079 NumItemsLT(S:ResRef\*,O:Object\*,I:Num\*)~
  APPEND ~trigger.ids~ ~0x407A NumItemsParty(S:ResRef*,I:Num*)~ UNLESS ~0x407A NumItemsParty(S:ResRef\*,I:Num\*)~
  APPEND ~trigger.ids~ ~0x407B NumItemsPartyGT(S:ResRef*,I:Num*)~ UNLESS ~0x407B NumItemsPartyGT(S:ResRef\*,I:Num\*)~
  APPEND ~trigger.ids~ ~0x407C NumItemsPartyLT(S:ResRef*,I:Num*)~ UNLESS ~0x407C NumItemsPartyLT(S:ResRef\*,I:Num\*)~
  APPEND ~trigger.ids~ ~0x407D IsOverMe(O:Object*)~ UNLESS ~0x407D IsOverMe(O:Object\*)~
END

ACTION_IF GAME_INCLUDES ~bg1~ BEGIN
  //small bonus: Boots of Stealth from Nadarin (quest to kill basilisk in Baldur's Gate) replaced with Boots of the West to match his dialog
  ACTION_IF NOT FILE_EXISTS_IN_GAME ~misc8j.itm~ BEGIN
    COPY ~%MOD_FOLDER%/core/items/misc8j.itm~ override
      SAY NAME1 @107
      SAY NAME1 @108
      SAY UNIDENTIFIED_DESC @109
      SAY IDENTIFIED_DESC @110
  END
  COPY_EXISTING ~nadari.cre~ ~override~
    ADD_CRE_ITEM ~misc8j~ #0 #0 #0 ~NONE~ ~INV10~
  BUT_ONLY
  OUTER_SET $item_source(~misc8j~ ~nadari.cre~) = 1
  OUTER_SET $item_source(~misc8j~ ~nadari.dlg~) = 1

<<<<<<<< .../ua-inlined/nadari.d
REPLACE_ACTION_TEXT ~nadari~
  ~GiveItem("boot02", LastTalkedToBy)~
  ~GiveItem("misc8j", LastTalkedToBy)~
>>>>>>>>
  COMPILE ~.../ua-inlined/nadari.d~


  //Kondar: make unstealable from Aldeth
  COPY_EXISTING ~aldeth.cre~ ~override~
    LPF ADD_CRE_ITEM_FLAGS
      STR_VAR
        item_to_change = ~sw1h03~
        flags = ~UNSTEALABLE~
    END
  BUT_ONLY


//Ordulinian reward for blood deeds switched from Cloak of Non-Detection to Cloak of Protection +1
<<<<<<<< .../ua-inlined/orduli.d
REPLACE_ACTION_TEXT ~orduli~ ~clck06~ ~clck01~
>>>>>>>>
  COMPILE ~.../ua-inlined/orduli.d~
END
