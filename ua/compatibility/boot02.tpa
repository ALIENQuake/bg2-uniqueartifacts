/*
Hobgoblin with Boots of Stealth appears in Trade Way North and Ulcaster, removing the one from TWN

BGEE/EET version handled separately, because Beamdog created a separate pair for boots just for that quest.

Only when installing Expanded option.
*/


/* There's no way to check current area in BAF
    And the hobgoblin doesn't have a script name in BGT.
    So instead we set an area variable. */
<<<<<<<< .../ua-inlined/SouthBeregostRoad.baf
  IF
    Global("g_hobgzhur_destroy_self", "MYAREA", 0)
  THEN
    RESPONSE #1
      SetGlobal("g_hobgzhur_destroy_self", "MYAREA", 1)
  END
>>>>>>>>
// ... and self destroy the hobgoblin based on that variable.
<<<<<<<< .../ua-inlined/g_hgzhur.baf
  IF
    Global("g_hobgzhur_destroy_self", "MYAREA", 1)
  THEN
    RESPONSE #1
      SetGlobal("g_hobgzhur_destroy_self", "MYAREA", 2)
      DestroySelf()
  END
>>>>>>>>


ACTION_IF GAME_INCLUDES ~bg1~ AND expanded == 1 BEGIN

    // Only the one at TWN gets destroyed
    EXTEND_TOP ~%SouthBeregostRoad_BCS%.bcs~ ~.../ua-inlined/SouthBeregostRoad.baf~

    // BGEE replaces boot02 with a special version, we're reverting it back
    ACTION_IF GAME_IS ~bgee eet~ BEGIN

        // item on cre
        COPY_EXISTING ~hobgzhur.cre~ ~override~
            LPF REPLACE_CRE_ITEM_MATCH STR_VAR match_item = ~boot02zh~ new_item = ~boot02~ END
        BUT_ONLY

        // revert Zhurlong's dialog to BG1 behaviour, Zhurlong will accept any Boots of Stealth
        COPY_EXISTING ~zhurlo.dlg~ ~override~
            DECOMPILE_AND_PATCH BEGIN
                REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~boot02zh~ ~boot02~
            END
        BUT_ONLY

        // enable new script
        COPY_EXISTING ~hobgzhur.cre~ ~override~
            READ_ASCII CRE_script_general script
        BUT_ONLY
        ACTION_IF FILE_EXISTS_IN_GAME ~%script%.bcs~ BEGIN
            EXTEND_TOP ~%script%.bcs~ ~.../ua-inlined/g_hgzhur.baf~
        END ELSE BEGIN
            COMPILE ~.../ua-inlined/g_hgzhur.baf~
            COPY_EXISTING ~hobgzhur.cre~ ~override~
                WRITE_ASCII CRE_script_general ~g_hgzhur~
            BUT_ONLY
        END
    END

END
