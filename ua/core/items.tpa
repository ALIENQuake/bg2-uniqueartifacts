// GAME_IS doesn't support variables
DEFINE_PATCH_MACRO read_item_sources_2da BEGIN
  READ_2DA_ENTRIES_NOW strict 2
  FOR (i=1;i<strict;++i) BEGIN
    READ_2DA_ENTRY_FORMER strict i 0 item
    READ_2DA_ENTRY_FORMER strict i 1 source
    // auto formatting workaround
    INNER_PATCH_SAVE fixed_source ~%source%~ BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~|$~ ~~
    END
    // skip comments
    PATCH_IF NOT (~%item%~ STRING_MATCHES_REGEXP ~^//~ == 0)
      AND NOT (~%item%~ STR_EQ ~|~) // auto formatting workaround
    BEGIN
      SPRINT $source_item("%fixed_source%") ~%item%~
      SPRINT $item_source("%item%") ~%fixed_source%~
    END
  END
END
DEFINE_ACTION_MACRO read_item_sources BEGIN
  COPY - ~%MOD_FOLDER%/items/strict_%game%.bsv~ ~%MOD_FOLDER%-inlined/strict_%game%.bsv~
    LPM read_item_sources_2da
  BUT_ONLY
  ACTION_IF expanded == 1 BEGIN
    COPY - ~%MOD_FOLDER%/items/expanded_%game%.bsv~ ~%MOD_FOLDER%-inlined/expanded_%game%.bsv~
      LPM read_item_sources_2da
    BUT_ONLY
  END
END

ACTION_IF GAME_INCLUDES ~bg1~ BEGIN
  OUTER_SPRINT game = ~bg1~
  LAM read_item_sources
END
ACTION_IF GAME_INCLUDES ~bg2~ BEGIN
  OUTER_SPRINT game = ~bg2~
  LAM read_item_sources
END

OUTER_SPRINT regex ~~
ACTION_PHP_EACH source_item AS source => item BEGIN
  OUTER_SET present = 0
  LAF IS_ITEM_IN_RESOURCE
    STR_VAR item = ~%item%~ resource = ~%source%~
    RET present = present
  END
  ACTION_IF present == 1 BEGIN
    ACTION_IF ~%regex%~ STR_EQ ~~ BEGIN //empty, initialize
      OUTER_TEXT_SPRINT regex ~%item%~
    END ELSE BEGIN
      OUTER_TEXT_SPRINT regex ~%regex%\|%item%~ //append
    END
  END ELSE BEGIN
    WARN ~UA WARNING: %item% is missing from %source%, skipping!~
  END
END
PRINT ~%regex%~
