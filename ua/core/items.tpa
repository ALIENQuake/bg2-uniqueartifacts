// GAME_IS doesn't support variables
DEFINE_PATCH_MACRO read_item_sources_2da BEGIN
  READ_2DA_ENTRIES_NOW strict 2
  FOR (i=1;i<strict;++i) BEGIN
    READ_2DA_ENTRY_FORMER strict i 0 item
    READ_2DA_ENTRY_FORMER strict i 1 source
    // auto formatting workaround
    INNER_PATCH_SAVE fixed_source ~%source%~ BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~|$~ ~~
    END
    PATCH_IF NOT (~%item%~ STRING_MATCHES_REGEXP ~^//~ == 0) // skip comments
      AND NOT (~%item%~ STR_EQ ~|~) // auto formatting workaround
    BEGIN
      SPRINT $sources(counter) ~%item%~
      SPRINT $item_present(~%item%~) 0
      counter+=1 // initialized in main.tpa
    END
  END
END
DEFINE_ACTION_MACRO read_item_sources BEGIN
  COPY - ~%MOD_FOLDER%/items/strict_%game%.bsv~ ~%MOD_FOLDER%-inlined/strict_%game%.bsv~
    LPM read_item_sources_2da
  BUT_ONLY
  ACTION_IF expanded == 1 BEGIN
    COPY - ~%MOD_FOLDER%/items/expanded_%game%.bsv~ ~%MOD_FOLDER%-inlined/expanded_%game%.bsv~
      LPM read_item_sources_2da
    BUT_ONLY
  END
END

ACTION_IF GAME_INCLUDES ~bg1~ BEGIN
  OUTER_SPRINT game ~bg1~
  LAM read_item_sources
END
ACTION_IF GAME_INCLUDES ~bg2~ BEGIN
  OUTER_SPRINT game ~bg2~
  LAM read_item_sources
END

// check if item is present in all sources
ACTION_PHP_EACH items_in_source AS item_counter => source BEGIN
  OUTER_SPRINT item $item_counter(0)
  OUTER_SET present = 0
  LAF IS_ITEM_IN_RESOURCE
    STR_VAR item = ~%item%~ resource = ~%source%~
    RET present = present
  END
  ACTION_IF present == 1 BEGIN
    OUTER_SPRINT $item_present(~%item%~) present
  END ELSE BEGIN
    WARN ~UA WARNING: %item% is missing from %source%, skipping!~
  END
END

// build a regex of all items, skip those that are not found in canon sources
OUTER_SPRINT regex_all_items ~~
ACTION_PHP_EACH items_in_source AS item_counter => source BEGIN
  OUTER_SPRINT item $item_counter(0)
  ACTION_IF $item_present$(~%item%~) == 1 BEGIN

    // common regex
    ACTION_IF ~%regex_all_items%~ STR_EQ ~~ BEGIN //empty, initialize
      OUTER_SPRINT regex_all_items ~%item%~
    END ELSE BEGIN
      OUTER_SPRINT regex_all_items ~%regex_all_items%\|%item%~ //append
    END

    // custom regex for each source
    ACTION_PHP_EACH sources AS int => isource BEGIN
      ACTION_IF NOT ~%isource%~ STR_EQ ~%source%~ BEGIN
        ACTION_IF VARIABLE_IS_SET $custom_regex(~%source%~) BEGIN
          OUTER_SPRINT tmp $custom_regex(~%source%~)
          OUTER_SPRINT $custom_regex(~%source%~) ~%tmp%\|%item%~
        END ELSE BEGIN
          OUTER_SPRINT $custom_regex(~%source%~) ~%item%~
        END
      END
    END

  END
END

PRINT ~all items: %regex_all_items%~
ACTION_PHP_EACH custom_regex AS source => cregex BEGIN
  PRINT ~custom: %source% => %cregex%~
END
