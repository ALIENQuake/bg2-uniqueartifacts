// GAME_IS doesn't support variables
DEFINE_PATCH_MACRO read_item_sources_2da BEGIN
  READ_2DA_ENTRIES_NOW strict 2
  FOR (i=1;i<strict;++i) BEGIN
    READ_2DA_ENTRY_FORMER strict i 0 item
    READ_2DA_ENTRY_FORMER strict i 1 source
    // auto formatting workaround
    INNER_PATCH_SAVE fixed_source ~%source%~ BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~|$~ ~~
    END
    TO_UPPER fixed_source
    PATCH_IF NOT (~%item%~ STRING_MATCHES_REGEXP ~^//~ == 0) // skip comments
      AND NOT (~%item%~ STR_EQ ~|~) // auto formatting workaround
    BEGIN
      SPRINT $sources(~%fixed_source%~) ~%counter%~ // list of all sources
      SET $item_present(~%item%~) = "-1" // track if item is present in all sources
      SPRINT $item_source(~%item%~ ~%fixed_source%~) ~%counter%~ // global list of items and sources
      counter+=1 // initialized in main.tpa, compatibility has effect on this
    END
  END
END
DEFINE_ACTION_MACRO read_item_sources BEGIN
  COPY - ~%MOD_FOLDER%/items/strict_%game%.bsv~ ~%MOD_FOLDER%-inlined/strict_%game%.bsv~
    LPM read_item_sources_2da
  BUT_ONLY
  ACTION_IF expanded == 1 BEGIN
    COPY - ~%MOD_FOLDER%/items/expanded_%game%.bsv~ ~%MOD_FOLDER%-inlined/expanded_%game%.bsv~
      LPM read_item_sources_2da
    BUT_ONLY
  END
END

ACTION_IF GAME_INCLUDES ~bg1~ BEGIN
  OUTER_SPRINT game ~bg1~
  LAM read_item_sources
END
ACTION_IF GAME_INCLUDES ~bg2~ BEGIN
  OUTER_SPRINT game ~bg2~
  LAM read_item_sources
END

// check if item is present in all sources
ACTION_PHP_EACH item_source AS i_s => int BEGIN
  OUTER_SPRINT item $i_s(0)
  OUTER_SPRINT source $i_s(1)
  ACTION_IF $item_present(~%item%~) == 0 BEGIN
    PRINT ~%item% was checked already and is missing from another resource, skipping~
  END ELSE BEGIN
    OUTER_SET present = 0
    LAF IS_ITEM_IN_RESOURCE STR_VAR item = ~%item%~ resource = ~%source%~ RET present END 
    OUTER_SET $item_present(~%item%~) = present
    ACTION_IF present == 0 BEGIN
      WARN ~UA WARNING: %item% is missing from %source%, skipping!~
    END
  END
END

// init custom regex for each source
ACTION_PHP_EACH sources AS csource => int BEGIN
  OUTER_SPRINT $custom_regex(~%csource%~) ~~
END

// build a regex of all items, skip those that are not found in canon sources
OUTER_SPRINT common_regex ~~
ACTION_PHP_EACH item_present AS item => present BEGIN
  ACTION_IF present == 1 BEGIN
    // common regex
    ACTION_IF ~%common_regex%~ STR_EQ ~~ BEGIN //empty, initialize
      OUTER_SPRINT common_regex ~%item%~
    END ELSE BEGIN
      OUTER_SPRINT common_regex ~%common_regex%\|%item%~ //append
    END
    // custom regex for each source
    ACTION_PHP_EACH sources AS csource => int BEGIN
      ACTION_IF NOT VARIABLE_IS_SET $item_source(~%item%~ ~%csource%~) BEGIN
        OUTER_SPRINT tmp $custom_regex(~%csource%~)
        ACTION_IF ~%tmp%~ STR_EQ ~~ BEGIN
          OUTER_SPRINT $custom_regex(~%csource%~) ~%item%~
        END ELSE BEGIN
          OUTER_SPRINT $custom_regex(~%csource%~) ~%tmp%\|%item%~
        END
      END
    END

  END
END
