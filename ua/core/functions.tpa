//Replace all actions which give the party "old_item" with a corresponding action and "new_item". "old_item" may be a regexp.
DEFINE_PATCH_FUNCTION REPLACE_D_AND_BAF_ITEM
  STR_VAR
    old_item = ~~
    new_item = ~~
BEGIN
  REPLACE_EVALUATE CASE_INSENSITIVE
    ~GiveItem("\(%old_item%\)"\(.*\))~
    BEGIN END
    ~GiveItem("%new_item%"%MATCH2%)~

  REPLACE_EVALUATE CASE_INSENSITIVE
    ~GiveItemCreate("\(%old_item%\)"\(.*\))~
    BEGIN END
    ~GiveItemCreate("%new_item%"%MATCH2%)~

  REPLACE_EVALUATE CASE_INSENSITIVE
    ~CreateItem("\(%old_item%\)"\(.*\))~
    BEGIN END
    ~CreateItem("%new_item%"%MATCH2%)~

  REPLACE_EVALUATE CASE_INSENSITIVE
    ~CreateItemGlobal("\(%old_item%\)"\(.*\))~
    BEGIN END
    ~CreateItemGlobal("%new_item%"%MATCH2%)~

  REPLACE_EVALUATE CASE_INSENSITIVE
    ~CreateItemNumGlobal("\(%old_item%\)"\(.*\))~
    BEGIN END
    ~CreateItemNumGlobal("%new_item%"%MATCH2%)~

  REPLACE_EVALUATE CASE_INSENSITIVE
    ~TakeItemReplace("\(%old_item%\)"\(.*\))~
    BEGIN END
    ~TakeItemReplace("%new_item%"%MATCH2%)~
END

DEFINE_ACTION_FUNCTION IS_ITEM_IN_RESOURCE
  STR_VAR
    item = ~~
    resource = ~~
  RET present
BEGIN
  OUTER_SET present = 0
  OUTER_PATCH_SAVE file_res ~%resource%~ BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE ~\.are$\|\.bcs$\|\.dlg$\|\.cre$\|\.sto$~ ~~
  END
  ACTION_IF ("%resource%" STRING_MATCHES_REGEXP ".*\.are$" = 0) BEGIN
    LAF IS_ITEM_IN_AREA
      STR_VAR item = ~%item%~ area = ~%file_res%~
      RET present
    END
  END
  ACTION_IF ("%resource%" STRING_MATCHES_REGEXP ".*\.cre$" = 0) BEGIN
    LAF IS_ITEM_ON_CREATURE
      STR_VAR item = ~%item%~ creature = ~%file_res%~
      RET present
    END
  END
  ACTION_IF ("%resource%" STRING_MATCHES_REGEXP ".*\.sto$" = 0) BEGIN
    LAF IS_ITEM_IN_STORE
      STR_VAR item = ~%item%~ store = ~%file_res%~
      RET present = present
    END
  END
  ACTION_IF ("%resource%" STRING_MATCHES_REGEXP ".*\.bcs$" = 0) BEGIN
    LAF IS_ITEM_GIVEN_OR_CREATED_IN_SCRIPT
      STR_VAR item = ~%item%~ script = ~%file_res%~
      RET present = present
    END
  END
  ACTION_IF ("%resource%" STRING_MATCHES_REGEXP ".*\.dlg$" = 0) BEGIN
    LAF IS_ITEM_GIVEN_OR_CREATED_IN_DIALOG
      STR_VAR item = ~%item%~ dialog = ~%file_res%~
      RET present = present
    END
  END
END

DEFINE_ACTION_FUNCTION IS_ITEM_IN_AREA
  STR_VAR
    item = ~~
    area = ~~
  RET present
BEGIN
  COPY_EXISTING ~%area%.are~ ~override~
    present = 0
    GET_OFFSET_ARRAY cont_offs ARE_V10_CONTAINERS
    PHP_EACH cont_offs AS int => c_off BEGIN
      GET_OFFSET_ARRAY2 item_offs c_off ARE_V10_ITEMS
      PHP_EACH item_offs AS int => i_off BEGIN
        READ_ASCII i_off item_name
        PATCH_IF ~%item%~ STR_EQ ~%item_name%~ BEGIN
          present = 1
        END
      END
    END
  BUT_ONLY
END

DEFINE_ACTION_FUNCTION IS_ITEM_ON_CREATURE
  STR_VAR
    item = ~~
    creature = ~~
  RET present
BEGIN
  COPY_EXISTING ~%creature%.cre~ ~override~
    present = 0
    GET_OFFSET_ARRAY item_offs CRE_V10_ITEMS
    PHP_EACH item_offs AS int => i_off BEGIN
      READ_ASCII i_off item_name
      PATCH_IF ~%item%~ STR_EQ ~%item_name%~ BEGIN
        present = 1
      END
    END
  BUT_ONLY
END

DEFINE_ACTION_FUNCTION IS_ITEM_IN_STORE
  STR_VAR
    item = ~~
    store = ~~
  RET present
BEGIN
  COPY_EXISTING ~%store%.sto~ ~override~
    present = 0
    GET_OFFSET_ARRAY item_offs STO_V10_ITEMS_SOLD
    PHP_EACH item_offs AS int => i_off BEGIN
      READ_ASCII i_off item_name
      PATCH_IF ~%item%~ STR_EQ ~%item_name%~ BEGIN
        present = 1
      END
    END
  BUT_ONLY
END

DEFINE_ACTION_FUNCTION IS_ITEM_GIVEN_OR_CREATED_IN_DIALOG_OR_SCRIPT
  STR_VAR
    item = ~~
    filename = ~~
  RET present
BEGIN
  COPY_EXISTING ~%filename%~ ~override~
    present = 0
    DECOMPILE_AND_PATCH BEGIN
      PATCH_IF
        INDEX_BUFFER (CASE_INSENSITIVE EXACT_MATCH ~GiveItem("%item%"~) != "-1"
        OR INDEX_BUFFER (CASE_INSENSITIVE EXACT_MATCH ~GiveItemCreate("%item%"~) != "-1"
        OR INDEX_BUFFER (CASE_INSENSITIVE EXACT_MATCH ~CreateItem("%item%"~) != "-1"
        OR INDEX_BUFFER (CASE_INSENSITIVE EXACT_MATCH ~CreateItemGlobal("%item%"~) != "-1"
        OR INDEX_BUFFER (CASE_INSENSITIVE EXACT_MATCH ~CreateItemNumGlobal("%item%"~) != "-1"
        OR INDEX_BUFFER (CASE_INSENSITIVE EXACT_MATCH ~TakeItemReplace("%item%"~) != "-1"
      BEGIN present = 1 END
    END
  BUT_ONLY
END

DEFINE_ACTION_FUNCTION IS_ITEM_GIVEN_OR_CREATED_IN_DIALOG
  STR_VAR
    item = ~~
    dialog = ~~
  RET present
BEGIN
  LAF IS_ITEM_GIVEN_OR_CREATED_IN_DIALOG_OR_SCRIPT STR_VAR item = ~%item%~ filename = ~%dialog%.dlg~ RET present END
END
DEFINE_ACTION_FUNCTION IS_ITEM_GIVEN_OR_CREATED_IN_SCRIPT
  STR_VAR
    item = ~~
    script = ~~
  RET present
BEGIN
  LAF IS_ITEM_GIVEN_OR_CREATED_IN_DIALOG_OR_SCRIPT STR_VAR item = ~%item%~ filename = ~%script%.bcs~ RET present END
END
